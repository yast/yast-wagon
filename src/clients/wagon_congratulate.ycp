/**
 * File:
 *	clients/wagon_congratulate.ycp
 *
 * Module:
 *	Wagon
 *
 * Authors:
 *	Lukas Ocilka <locilka@suse.cz>
 *
 * Summary:
 *	Online Migration Tool
 *
 * $Id$
 *
 */

{
    import "ProductControl";
    import "Wizard";
    import "Popup";
    import "GetInstArgs";
    import "Label";

    textdomain "wagon";

    symbol ret = `auto;

    map display = UI::GetDisplayInfo();
    integer space = display["TextMode"]:true ? 1 : 3;

    string caption = _("Migration Completed");

    string text = ProductControl::GetTranslatedText ("migration_congratulate");

    if (text == nil || text == "") {
	y2warning ("Using fallback migration_congratulate text");
	// translators: %1 is a URL, e.g. http://www.suse.com
	text = sformat(_("<p><b>Congratulations!</b><br>
You have successfully finished the on-line migration.</p>
<p>The whole system has been upgraded. It should be rebooted
as soon as possible.</p>
<p>Please visit us at %1.</p>
<p>Have a nice day!<br>
Your SUSE Linux Team</p>
"), "http://www.suse.com");
    }

    term contents = `VBox (
	`VSpacing (space),
	`HBox (
	    `HSpacing (2*space),
	    `VBox (
		`RichText (`id (`text), text)
	    ),
	    `HSpacing (2*space)
	),
	`VSpacing (space),
	`VSpacing (2)
    );

    string help = _("<p><b>Finish</b> will close the migration.
Restart the system as soon as possible.</p>
");

    Wizard::SetContents (caption, contents, help,
            GetInstArgs::enable_back(), GetInstArgs::enable_next());
    Wizard::SetTitleIcon ("yast-license");
                                          
    Wizard::SetNextButton(`next, Label::FinishButton() );
    Wizard::RestoreAbortButton();
    Wizard::SetFocusToNextButton();

    repeat {
	ret = (symbol) Wizard::UserInput();

	if (ret == `abort) {
	    if (Popup::ConfirmAbort (`incomplete))
	    break;
	}
    } until (ret == `next || ret == `back);

    if (ret == `back) {
	Wizard::RestoreNextButton();
    } else if (ret == `next) {
	Wizard::SetContents (caption, `Label(_("Finishing the migration...")), help,
	    GetInstArgs::enable_back(), GetInstArgs::enable_next());

	Pkg::SourceSaveAll();
	Pkg::TargetFinish();
    }

    return ret;
}
