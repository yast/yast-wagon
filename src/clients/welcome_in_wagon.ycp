/**
 * File:
 *	clients/welcome_in_wagon.ycp
 *
 * Module:
 *	Wagon
 *
 * Authors:
 *	Lukas Ocilka <locilka@suse.cz>
 *
 * Summary:
 *	Online Migration Tool
 *
 * $Id$
 *
 */

{
    import "Wizard";
    import "GetInstArgs";
    import "Popup";
    import "Wagon";
    import "Report";
    import "FileUtils";

    textdomain "wagon";

    // heading text
    string heading_text = _("Welcome");

    term contents = `VBox (
	`Label (_("This tool will help to update the
running system to a service pack.

Click 'Next' to start the update."))
    );

    // help text
    string help_text = _("<p>This tool updates the running system to a service pack.</p>");

    Wizard::SetContents (heading_text, contents, help_text,
	GetInstArgs::enable_back(), GetInstArgs::enable_next());
    Wizard::EnableAbortButton ();

    // file /etc/sysconfig/rhn/systemid means the system is managed by SUSE Manager
    if (FileUtils::Exists("/etc/sysconfig/rhn/systemid"))
    {
	y2milestone("File /etc/sysconfig/rhn/systemid found, aborting wagon");

	// warning message, system managed by SUSE Manager cannot be migrated by wagon
	// display this message and exit
	Popup::Message(_("This system is managed by SUSE Manager,\nYaST wagon module cannot migrate systems managed by SUSE Manager.\n\nPress OK to exit."));

	return (any)`back;
    }

    any ret = nil;

    while (true) {
	ret = UI::UserInput();

	if (ret == `back) {
	    break;
	} else if ((ret == `abort || ret == `cancel) && Popup::ConfirmAbort (`painless)) {
	    ret = `abort;
	    break;
	} else if (ret == `next) {
	    if (Wagon::InitPkg() != true) {
		// Report error but let user go further
		// Might help us in the future
		Report::Error (_("Cannot initialize package manager."));
	    }
	    break;
	} else {
	    y2error ("Unknown ret: %1", ret);
	}
    }

    // Clear the dialog
    Wizard::SetContents ("", `Empty(), "", true, true);

    return ret;
}
